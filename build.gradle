plugins {
  id "java"
  id "com.diffplug.spotless" version "6.13.0"
  id "com.github.johnrengelman.shadow" version "8.1.0"
}

group "io.tabular.iceberg.connect"
version "0.0.1"

repositories {
  mavenCentral()
}

dependencies {
  implementation libs.bundles.iceberg
  implementation libs.bundles.aws
  implementation libs.bundles.jackson
  implementation libs.hadoop.common
  implementation libs.avro
  compileOnly libs.bundles.kafka.connect

  testImplementation libs.junit.api
  testRuntimeOnly libs.junit.engine

  testImplementation libs.mockito
  testImplementation libs.assertj
  testImplementation libs.awaitility
  testImplementation libs.testcontainers
  testImplementation libs.testcontainers.kafka
  testImplementation libs.kafka.clients
  testImplementation libs.http.client
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// build the jar first so it can be installed into kafka connect
test.dependsOn(shadowJar)

test {
  useJUnitPlatform()
}

configurations {
  testImplementation.extendsFrom compileOnly
}

shadowJar {
  zip64 true
}

task copyShadowJar(type: Copy) {
  from shadowJar
  into "${buildDir}/out"
}

shadowJar.finalizedBy copyShadowJar

spotless {
  java {
    target "src/**/*.java"
    googleJavaFormat("1.7")
    removeUnusedImports()
    licenseHeaderFile "$rootDir/LICENSE.txt"
  }
}
