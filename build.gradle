plugins {
  id "java"
  id "com.diffplug.spotless" version "6.18.0"
  id "com.github.johnrengelman.shadow" version "8.1.0"
}

group "io.tabular.connect"
version "0.0.1"

repositories {
  mavenCentral()
  maven {
    url "https://tabular-repository-public.s3.amazonaws.com/releases"
  }
}

dependencies {
  implementation libs.bundles.iceberg
  implementation libs.bundles.aws
  implementation libs.hadoop.common
  compileOnly libs.kafka.connect.api
  compileOnly libs.lombok
  annotationProcessor libs.lombok

  testImplementation libs.junit.api
  testRuntimeOnly libs.junit.engine
  testImplementation libs.mockito
  testImplementation libs.assertj
  testImplementation libs.awaitility
  testImplementation libs.testcontainers
  testImplementation libs.testcontainers.debezium
  testImplementation libs.testcontainers.kafka
  testImplementation libs.kafka.clients
  testRuntimeOnly libs.h2

  testCompileOnly libs.lombok
  testAnnotationProcessor libs.lombok

}

compileJava {
  options.release.set(11) // TODO: Debezium image uses java 11
}

// build the jar first so it can be installed into kafka connect
test.dependsOn(shadowJar)

test {
  useJUnitPlatform()
}

shadowJar {
  zip64 true
}

task copyShadowJar(type: Copy) {
  from shadowJar
  into "${buildDir}/out"
}

shadowJar.finalizedBy copyShadowJar

spotless {
  java {
    target "src/**/*.java"
    googleJavaFormat()
    licenseHeaderFile "$rootDir/LICENSE.txt"
  }
}
