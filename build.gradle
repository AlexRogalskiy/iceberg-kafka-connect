plugins {
  id "java"
  id "distribution"
  id "com.diffplug.spotless" version "6.13.0"
}

group "io.tabular.iceberg.connect"
version "0.0.1"

repositories {
  mavenCentral()
}

dependencies {
  implementation libs.bundles.iceberg
  implementation libs.bundles.aws
  implementation libs.bundles.jackson
  implementation libs.avro
  compileOnly libs.bundles.kafka.connect
  runtimeOnly libs.hadoop.common // REST catalog needs this when loading the FileIO

  testImplementation libs.junit.api
  testRuntimeOnly libs.junit.engine

  testImplementation libs.mockito
  testImplementation libs.assertj
  testImplementation libs.awaitility
  testImplementation libs.testcontainers
  testImplementation libs.testcontainers.kafka
  testImplementation libs.http.client
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'


test {
  useJUnitPlatform()
}

configurations {
  testImplementation.extendsFrom compileOnly
}

distributions {
  main {
    contents {
      into('lib/') {
        from jar
        from configurations.runtimeClasspath
      }
      from(processResources.destinationDir) {
        include 'manifest.json'
      }
    }
  }
}
tasks.distTar.enabled = false

spotless {
  java {
    target "src/**/*.java"
    googleJavaFormat("1.7")
    removeUnusedImports()
    licenseHeaderFile "$rootDir/LICENSE.txt"
  }
}

// build the distribution first so it can be installed into kafka connect
test.dependsOn installDist
assemble.dependsOn installDist
